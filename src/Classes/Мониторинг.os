#Использовать 1commands
#Использовать logos
#Использовать messenger

// Имя файла в который будет записываться лог
Перем ИмяФайлаЛога;
// Переменная для возврата ошибки, если таковая имела место быть
Перем ТекстОшибки;
// Строка для разделением уровней (отступ), по умолчанию - табуляция
Перем РазделительУровней;
// Количество строк разделения уровней, добавленных в текущую строку лога
Перем Уровень;
// При установки в значение Истина, добавляет текущую дату и время в начало каждой строки
Перем ДатаВремяВКаждойСтроке;
// Вывод сообщений при записи в лог, при установки в значение Истина, все строки лога будут выведены в сообщении
Перем ВыводитьСообщенияПриЗаписи;
// Признак использования мониторинга Zabbix
Перем ИспользоватьМониторинг;
// Расположение отправителя Zabbix Sender
Перем Отправитель;
// IP адрес Zabbix сервера
Перем АдресСервера;
// Номер порта Zabbix сервера
Перем Порт;
// Узел сети
Перем Узел;
// Ключ элемента данных
Перем Ключ;
// Отправка через файл-вложение
Перем ОтправкаЧерезФайл;
// Параметры отправки через телеграм
Перем ПараметрыТелеграм;
// Отправлять сообщения отладки
Перем ОтправлятьСообщенияОтладки;
// Параметры influx
Перем ПараметрыМетрик;

Процедура ИнициироватьПараметры()
	
	ИмяФайлаЛога = "";
	ТекстОшибки = "";
	Уровень = 0;
	ДатаВремяВКаждойСтроке = Истина;
	ВыводитьСообщенияПриЗаписи = Истина;
	РазделительУровней = "	";
	ИспользоватьМониторинг = Ложь;
	Отправитель = "C:\Program Files\Zabbix Agent\zabbix_sender.exe";
	Порт = 10051;
	ОтправкаЧерезФайл = Истина;
	ПараметрыТелеграм = Новый Структура("Отправка", Ложь);
	ПараметрыМетрик = Новый Структура("Отправка", Ложь);
	ОтправлятьСообщенияОтладки = Ложь;
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыТелеграм(BotID, ChatID, ТолькоОшибки = Ложь) Экспорт
	
	Мессенджер = Новый Мессенджер();
	Мессенджер.ИнициализироватьТранспорт("telegram", Новый Структура("Логин", BotID));
	
	ПараметрыТелеграм.Отправка = Истина;
	ПараметрыТелеграм.Вставить("Мессенджер", Мессенджер);
	ПараметрыТелеграм.Вставить("ChatID", ChatID);
	ПараметрыТелеграм.Вставить("ТолькоОшибки", ТолькоОшибки);
	
КонецПроцедуры

Процедура ЗаполнитьПараметрыМетрик(Хост, Порт, Логин, Пароль, ТекстЗапроса) Экспорт
	
	ПараметрыМетрик.Отправка = Истина;
	ПараметрыМетрик.Вставить("Хост", 			Хост);
	ПараметрыМетрик.Вставить("Порт", 			Порт);
	ПараметрыМетрик.Вставить("Логин", 			Логин);
	ПараметрыМетрик.Вставить("Пароль", 			Пароль);
	ПараметрыМетрик.Вставить("ТекстЗапроса", 	ТекстЗапроса);
	
КонецПроцедуры

Процедура УстановитьПараметрыЛогирования(ВыводитьВремя = Ложь, ВыводитьСообщения = Ложь,
		ЗначениеРазделителя = "	") Экспорт
	
	ДатаВремяВКаждойСтроке = ВыводитьВремя;
	ВыводитьСообщенияПриЗаписи = ВыводитьСообщения;
	РазделительУровней = ЗначениеРазделителя;
	
КонецПроцедуры

Процедура УстановитьПараметрыМониторинга(Адрес, ПолныйПутьСендера = Неопределено, НомерПорта = Неопределено) Экспорт
	
	АдресСервера = Адрес;
	Если НомерПорта <> Неопределено Тогда
		
		Порт = НомерПорта;
		
	КонецЕсли;
	
	Если ПолныйПутьСендера <> Неопределено Тогда
		
		Отправитель = ПолныйПутьСендера;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПараметрыЭлементаДанных(ИмяУзла, ИмяКлюча) Экспорт
	
	ИспользоватьМониторинг = Истина;
	Узел = ИмяУзла;
	Ключ = ИмяКлюча;
	
КонецПроцедуры

Процедура ОтправитьЗначениеМониторинга(Значение) Экспорт
	
	Команда = Новый Команда;
	
	Команда.УстановитьКоманду(Отправитель);
	Команда.ДобавитьПараметр(СтрШаблон("-z %1", АдресСервера));
	Команда.ДобавитьПараметр(СтрШаблон("-p %1", Порт));
	Если ОтправкаЧерезФайл Тогда
		
		ИмяФайла = ПолучитьИмяВременногоФайла("txt");
		
		ЗаписьТекста = Новый ЗаписьТекста(ИмяФайла, КодировкаТекста.UTF8NoBOM);
		СтрокаЛога = СтрШаблон("%1 %2 %3",
				Команда.ОбернутьВКавычки(Узел), Команда.ОбернутьВКавычки(Ключ), Команда.ОбернутьВКавычки(Значение));
		ЗаписьТекста.Записать(СтрокаЛога);
		ЗаписьТекста.Закрыть();
		
		Команда.ДобавитьПараметр(СтрШаблон("-i %1", Команда.ОбернутьВКавычки(ИмяФайла)));
		
	Иначе
		
		Команда.ДобавитьПараметр(СтрШаблон("-s %1", Команда.ОбернутьВКавычки(Узел)));
		Команда.ДобавитьПараметр(СтрШаблон("-k %1", Ключ));
		Команда.ДобавитьПараметр(СтрШаблон("-o %1", Команда.ОбернутьВКавычки(Значение)));
		
	КонецЕсли;
	
	Команда.Исполнить();
	
	Если ОтправкаЧерезФайл Тогда
		
		УдалитьФайлы(ИмяФайла);
		
	КонецЕсли;
	
КонецПроцедуры

Функция СоздатьФайлЛога(ИмяЗадания, Знач ИмяКаталога) Экспорт
	
	ТекстОшибки = "";
	
	Попытка
		
		ФорматированнаяДата = Формат(ТекущаяДата(), "ДФ=dd_MM_yyyy_ЧЧ_мм_сс");
		СоздатьКаталог(ОбъединитьПути(ИмяКаталога, ИмяЗадания));
		ИмяФайлаЛога = ОбъединитьПути(ИмяКаталога, ИмяЗадания, "Лог_" + ИмяЗадания + "_" +
				ФорматированнаяДата + ".txt");
		Документ = Новый ТекстовыйДокумент;
		Документ.Записать(ИмяФайлаЛога);
		Файл = Новый Файл(ИмяФайлаЛога);
		ИмяФайлаЛога = Файл.ПолноеИмя;
		Возврат Истина;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

Функция ПолучитьИмяФайлаЛога() Экспорт
	
	Возврат ИмяФайлаЛога;
	
КонецФункции

Процедура Информация(Запись) Экспорт
	
	ДобавитьЗаписьМониторинга(Запись, "INFO");
	
КонецПроцедуры

Процедура Ошибка(Запись) Экспорт
	
	ДобавитьЗаписьМониторинга(Запись, "ERROR");
	
КонецПроцедуры

Процедура КритическаяОшибка(Запись) Экспорт
	
	ДобавитьЗаписьМониторинга(Запись, "FATAL");
	
КонецПроцедуры

Процедура Внимание(Запись) Экспорт
	
	ДобавитьЗаписьМониторинга(Запись, "WARN");
	
КонецПроцедуры

Процедура Отладка(Запись) Экспорт
	
	ДобавитьЗаписьМониторинга(Запись, "DEBUG");
	
КонецПроцедуры

Процедура ДобавитьЗаписьМониторинга(Запись, Статус, ДобавитьУровеней = 0)
	
	Если ОтправлятьСообщенияОтладки ИЛИ Статус <> "DEBUG" Тогда
		
		Если ИспользоватьМониторинг Тогда
			
			СообщениеМониторинга = СтрШаблон("%1 %2", Статус, Запись);
			ОтправитьЗначениеМониторинга(СообщениеМониторинга);
			
		КонецЕсли;
		
		Если ПараметрыТелеграм.Отправка = Истина И Статус <> "DEBUG" Тогда
			
			ОтправитьВТелеграм(Запись, Статус);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстОшибки = "";
	
	Если НЕ ЗначениеЗаполнено(ИмяФайлаЛога) Тогда
		
		ТекстОшибки = "Файла не создан!";
		
	КонецЕсли;
	
	Для Сч = 1 По Уровень + ДобавитьУровеней Цикл
		
		Запись = РазделительУровней + Запись;
		
	КонецЦикла;
	
	Если ДатаВремяВКаждойСтроке Тогда
		
		Запись = "[" + Формат(ТекущаяДата(), "ДФ='dd.MM.yyyy ЧЧ:мм:сс'") + "] " + Запись;
		
	КонецЕсли;
	
	Попытка
		
		Если ИмяФайлаЛога <> "" Тогда
			
			ЗаписьТекста = Новый ЗаписьТекста;
			ЗаписьТекста.Открыть(ИмяФайлаЛога, , , Истина);
			ЗаписьТекста.ЗаписатьСтроку(Запись);
			ЗаписьТекста.Закрыть();
			
		КонецЕсли;
		
		Если ВыводитьСообщенияПриЗаписи Тогда
			
			Сообщить(Запись);
			
		КонецЕсли;
		
	Исключение
		
		ТекстОшибки = ОписаниеОшибки();
		
	КонецПопытки;
	
КонецПроцедуры

Процедура ОтправитьВТелеграм(Запись, Статус)
	
	Если Не ПараметрыТелеграм.ТолькоОшибки Тогда
		
		ПараметрыТелеграм.Мессенджер.ОтправитьСообщение("telegram", ПараметрыТелеграм.ChatID, Запись);
		
	КонецЕсли;
	
	Если ПараметрыТелеграм.ТолькоОшибки И (Статус = "ERROR" ИЛИ Статус = "FATAL") Тогда
		
		ПараметрыТелеграм.Мессенджер.ОтправитьСообщение("telegram", ПараметрыТелеграм.ChatID, Запись);
		
	КонецЕсли;
	
КонецПроцедуры

Функция РезультатОтправкиМетрики(Метрика) Экспорт
	
	Результат = Ложь;
	
	Если Не ПараметрыМетрик.Отправка Тогда
		
		Возврат Результат;
		
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(ПараметрыМетрик.Хост, ПараметрыМетрик.Порт,
			ПараметрыМетрик.Логин, ПараметрыМетрик.Пароль, , 5);
	
	Запрос = Новый HTTPЗапрос(ПараметрыМетрик.ТекстЗапроса);
	Запрос.УстановитьТелоИзСтроки(Метрика);
	
	Попытка
		
		Ответ = Соединение.ВызватьHTTPМетод("POST", Запрос);
		
		Успешно = 204;
		
		Если Ответ.КодСостояния = Успешно Тогда
			
			Результат = Истина;
			
		КонецЕсли;
		
	Исключение
		
		Результат = Ложь;
		
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

Функция ДатаВСтрокуUNIX(Дата, Делитель = 1000) Экспорт
	
	Значение = (УниверсальноеВремя(Дата) - '19700101') * Делитель;
	
	Возврат Значение;
	
КонецФункции

Процедура УвеличитьУровень(ДобавитьУровеней = 1) Экспорт
	
	Уровень = Уровень + ДобавитьУровеней;
	
КонецПроцедуры

Процедура УменьшитьУровень(ВычестьУровеней = 1) Экспорт
	
	Уровень = Уровень - ВычестьУровеней;
	
КонецПроцедуры

Процедура УстановитьНулевойУровень() Экспорт
	
	Уровень = 0;
	
КонецПроцедуры

ИнициироватьПараметры();